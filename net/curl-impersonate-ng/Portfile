# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem      1.0
PortGroup       conflicts_build 1.0
PortGroup       github 1.0

name            curl-impersonate-ng
github.setup    lexiforest curl-impersonate 1.3.1 v
revision        0
categories      net www
maintainers     {@barracuda156 gmail.com:vital.had} openmaintainer
license         MIT

description     An active fork of curl-impersonate with more versions and build targets
long_description \
                {*}${description}

distname        ${github.project}-${version}
github.tarball_from archive

# FIXME: curl-impersonate executable looks fine and works, but static library may be
# missing some symbols expected by dependents: https://github.com/lexiforest/curl_cffi/issues/696

# Specific versions: https://github.com/lexiforest/curl-impersonate/blob/6571b771f192443f584c4bcd6a1e405f62493688/Makefile.in
set boringssl_hash      673e61fc215b178a90c0e67858bbf162c8158993
set brotli_version      1.1.0
set curl_version        8.15.0
set nghttp2_version     1.63.0
set nghttp3_version     1.9.0
set ngtcp2_version      1.11.0

set curl_tag            [string map {. _} ${curl_version}]

distfiles-append \
                ${boringssl_hash}${extract.suffix}:boringssl \
                v${brotli_version}${extract.suffix}:brotli \
                curl-${curl_tag}${extract.suffix}:curl \
                v${nghttp2_version}${extract.suffix}:nghttp2 \
                nghttp3-${nghttp3_version}${extract.suffix}:nghttp3 \
                ngtcp2-${ngtcp2_version}${extract.suffix}:ngtcp2

master_sites-append \
                https://github.com/google/boringssl/archive/:boringssl \
                https://github.com/google/brotli/archive/refs/tags/:brotli \
                https://github.com/curl/curl/archive/refs/tags/:curl \
                https://github.com/nghttp2/nghttp2/archive/refs/tags/:nghttp2 \
                https://github.com/ngtcp2/nghttp3/releases/download/v${nghttp3_version}/:nghttp3 \
                https://github.com/ngtcp2/ngtcp2/releases/download/v${ngtcp2_version}/:ngtcp2

checksums       ${distname}${extract.suffix} \
                rmd160  d0e71640a2a7151a714c19f5584edb1ef31715cf \
                sha256  dcc2f4ab4c96d6a1e2711dd2f9253f790af2c116978bdb6227b82eef6eda9281 \
                size    173231 \
                ${boringssl_hash}${extract.suffix} \
                rmd160  39c6a39e50c4eb019378f8077f0bbfae6d7184a1 \
                sha256  5f84f1d01a278a5cadf51acc11a0c4519f2f57277cff33d154917083c68463b4 \
                size    45421885 \
                v${brotli_version}${extract.suffix} \
                rmd160  a74b7a2e3014e7252f297a504894b3b135b8903e \
                sha256  e720a6ca29428b803f4ad165371771f5398faba397edf6778837a18599ea13ff \
                size    511969 \
                curl-${curl_tag}${extract.suffix} \
                rmd160  c3e8904c45aaaa8df29897277fb8c41303888020 \
                sha256  2937cadde007aa3a52a17c21ac9153ea054700f37926d1d96602bf07e888c847 \
                size    3483257 \
                v${nghttp2_version}${extract.suffix} \
                rmd160  fa3d04976a104cf66338997f6fa55d1c465623d6 \
                sha256  f3da0627bee7a6a60f5a4eb6de8d17d25e99f50f87b0fc0c20676c682bf31098 \
                size    1070155 \
                nghttp3-${nghttp3_version}${extract.suffix} \
                rmd160  0601507ff45b2662a2bea76e4ee74a42eca27464 \
                sha256  dbc7ad9e5dfa4788439e1e5fe072345fe2a48f68516374937be95c6a893451bd \
                size    642668 \
                ngtcp2-${ngtcp2_version}${extract.suffix} \
                rmd160  47134747909f5a77ed211c1fcf3002aca1ba84c7 \
                sha256  41fc390d07b89e2285e1107279518e755cdd5cd046630da3301a9cadcfc9e4b6 \
                size    1141749

# Use naming which Makefile expects, avoid using broken targets:
post-extract {
    xinstall -d ${worksrcpath}/build

    ln -s ${workpath}/boringssl-${boringssl_hash} ${worksrcpath}/build/boringssl-${boringssl_hash}
    ln -s ${workpath}/brotli-${brotli_version} ${worksrcpath}/build/brotli-${brotli_version}
    ln -s ${workpath}/curl-curl-${curl_tag} ${worksrcpath}/build/curl-${curl_tag}
    ln -s ${workpath}/nghttp2-${nghttp2_version} ${worksrcpath}/build/nghttp2-${nghttp2_version}
    ln -s ${workpath}/nghttp3-${nghttp3_version} ${worksrcpath}/build/nghttp3-${nghttp3_version}
    ln -s ${workpath}/ngtcp2-${ngtcp2_version} ${worksrcpath}/build/ngtcp2-${ngtcp2_version}
}

set py_ver          3.13
set py_ver_nodot    [string map {. {}} ${py_ver}]
configure.python    ${prefix}/bin/python${py_ver}

depends_build-append \
                port:autoconf \
                port:automake \
                port:bash \
                path:bin/cmake:cmake \
                port:libtool \
                port:ninja \
                path:bin/perl:perl5 \
                path:bin/pkg-config:pkgconfig
depends_lib-append \
                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle \
                port:libidn2 \
                port:sqlite3 \
                port:zlib \
                port:zstd

# This is ugly, but to be on a safe side:
conflicts_build         openssl

compiler.c_standard     2011
compiler.cxx_standard   2020

# Apply patches included in ${name}:
pre-patch {
    # Upstream patches:
    system -W ${workpath}/boringssl-${boringssl_hash} "patch -p1 < [shellescape ${worksrcpath}/patches/boringssl.patch]"
    system -W ${workpath}/curl-curl-${curl_tag} "patch -p1 < [shellescape ${worksrcpath}/patches/curl.patch]"
    # Our patches. At the moment it switches to urandom to avoid depending on getentropy.
    if {${os.platform} eq "darwin" && ${os.major} < 16} {
        system -W ${workpath}/boringssl-${boringssl_hash} "patch -p0 < [shellescape ${filespath}/0001-boringssl.patch]"
    }
}

patch.pre_args-replace  -p0 -p1

patchfiles-append   0002-Fix-Makefile.patch

post-patch {
    reinplace "s|@CC@|${configure.cc}|g" ${worksrcpath}/Makefile.in
    if {${configure.cxx_stdlib} eq "libc++"} {
        reinplace "s|@CXXLIB@|c++|" ${worksrcpath}/Makefile.in
    } else {
        reinplace "s|@CXXLIB@|stdc++|" ${worksrcpath}/Makefile.in
    }
    if {${os.platform} eq "darwin" && ${os.major} < 11} {
        reinplace "s|\$(CURL_CONFIG_FLAGS)|--without-secure-transport|g" ${worksrcpath}/Makefile.in
    }
}

configure.dir   ${worksrcpath}/build
configure.cmd   ../configure

configure.env-append \
                SHELL=${prefix}/bin/bash

configure.args-append \
                --enable-static \
                --prefix=${destroot}${prefix} \
                --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt \
                --with-zlib=${prefix} \
                --with-zstd=${prefix}

default extra_flags {}

# O_CLOEXEC
if {${os.platform} eq "darwin" && ${os.major} < 11} {
    depends_build-append \
                    path:lib/libMacportsLegacySupport.a:legacy-support
    set extra_flags -isystem${prefix}/include/LegacySupport\ -Wl\,${prefix}/lib/libMacportsLegacySupport.a
}

# Avoid nightmarish makefile which cannot even configure sub-projects correctly.
pre-build {
    xinstall -d ${workpath}/brotli-${brotli_version}/out
    system -W ${workpath}/brotli-${brotli_version}/out "${prefix}/bin/cmake \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=\./installed -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_COMPILER=${configure.cc} -DCMAKE_CXX_COMPILER=${configure.cxx} \
        -DCMAKE_C_FLAGS='${configure.cflags}' -DCMAKE_CXX_FLAGS='${configure.cxxflags}' \
        -DCMAKE_OSX_ARCHITECTURES=${configure.build_arch} -DCMAKE_OSX_DEPLOYMENT_TARGET=${macosx_deployment_target} \.\. && \
        ${prefix}/bin/cmake --build \. -v --config Release --target install"

    set ssl_dir ${workpath}/boringssl-${boringssl_hash}
    set ssl_build_dir ${ssl_dir}/build
    xinstall -d ${ssl_build_dir}
    xinstall -d ${ssl_dir}/lib
    # Build arbitrarily picks iOS code: ios.cc:23:10: fatal error: CommonCrypto/CommonRandom.h: No such file or directory
    system -W ${ssl_build_dir} "${prefix}/bin/cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_POSITION_INDEPENDENT_CODE=on -DOPENSSL_NO_ASM=on -DFIPS=off \
        -DCMAKE_C_COMPILER=${configure.cc} -DCMAKE_CXX_COMPILER=${configure.cxx} \
        -DCMAKE_C_FLAGS='${configure.cflags} -DTARGET_OS_OSX=1 ${extra_flags} -Wno-unknown-warning-option -Wno-stringop-overflow -Wno-array-bounds -Wno-macro-redefined' \
        -DCMAKE_CXX_FLAGS='${configure.cxxflags} -DTARGET_OS_OSX=1 -D__STDC_FORMAT_MACROS ${extra_flags} -Wno-macro-redefined' \
        -DCMAKE_OSX_ARCHITECTURES=${configure.build_arch} -DCMAKE_OSX_DEPLOYMENT_TARGET=${macosx_deployment_target} \
        -GNinja \.\. && ${prefix}/bin/ninja -v"
    copy ${ssl_build_dir}/crypto/libcrypto.a ${ssl_dir}/lib/libcrypto.a
    copy ${ssl_build_dir}/ssl/libssl.a ${ssl_dir}/lib/libssl.a

    system -W ${workpath}/nghttp2-${nghttp2_version} "autoreconf -i && ./configure \
        --prefix=${workpath}/nghttp2-${nghttp2_version}/installed --with-pic --enable-lib-only --disable-shared && \
        make V=1 && make install"

    system -W ${workpath}/nghttp3-${nghttp3_version} "autoreconf -i && \
        CC=${configure.cc} CXX=${configure.cxx} ./configure \
        --prefix=${workpath}/nghttp3-${nghttp3_version}/installed --with-pic --enable-lib-only --disable-shared && \
        make V=1 && make install"

    system -W ${workpath}/ngtcp2-${ngtcp2_version} "autoreconf -i && \
        CC=${configure.cc} CXX=${configure.cxx} ./configure \
        PKG_CONFIG_PATH=nghttp3-${nghttp3_version}/installed/lib/pkgconfig \
        BORINGSSL_LIBS='-L${ssl_dir}/lib -lssl -lcrypto -lpthread' \
        BORINGSSL_CFLAGS='-I${ssl_dir}/include' --with-boringssl \
        --prefix=${workpath}/ngtcp2-${ngtcp2_version}/installed --with-pic --enable-lib-only --disable-shared && \
        make V=1 && make install"
}

build.dir       ${worksrcpath}/build
build.target    build
build.args      V=1

destroot {
    system -W ${worksrcpath}/build "make install"
}
