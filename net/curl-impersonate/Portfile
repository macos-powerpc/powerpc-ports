# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem      1.0
PortGroup       conflicts_build 1.0
PortGroup       github 1.0

github.setup    lwthiker curl-impersonate 0.6.1 v
revision        0
categories      net www
maintainers     {@barracuda156 gmail.com:vital.had} openmaintainer
license         MIT

description     A special build of curl that can impersonate Chrome & Firefox
long_description \
                {*}${description}

github.tarball_from archive

# TODO: perhaps these do not need to conflict, but we cannot verify that atm.
variant chrome conflicts ff description "Impersonate Chrome" { }
variant ff conflicts chrome description "Impersonate FireFox" { }

# Specific versions: https://github.com/lwthiker/${name}/blob/v${version}/Makefile.in
set boringssl_hash      1b7fdbd9101dedc3e0aa3fcf4ff74eacddb34ecc
set brotli_version      1.0.9
set curl_version        8.1.1
set nghttp2_version     1.56.0
set nspr_version        4.35
set nss_version         3.92

set curl_tag            [string map {. _} ${curl_version}]
set nss_tag             [string map {. _} ${nss_version}]

distfiles-append \
                ${boringssl_hash}${extract.suffix}:boringssl \
                v${brotli_version}${extract.suffix}:brotli \
                curl-${curl_tag}${extract.suffix}:curl \
                v${nghttp2_version}${extract.suffix}:nghttp2 \
                nss-${nss_version}-with-nspr-${nspr_version}${extract.suffix}:nss

master_sites-append \
                https://github.com/google/boringssl/archive/:boringssl \
                https://github.com/google/brotli/archive/refs/tags/:brotli \
                https://github.com/curl/curl/archive/refs/tags/:curl \
                https://github.com/nghttp2/nghttp2/archive/refs/tags/:nghttp2 \
                https://ftp.mozilla.org/pub/security/nss/releases/NSS_${nss_tag}_RTM/src/:nss

checksums       ${distname}${extract.suffix} \
                rmd160  61f0ac37d35efe920c54e960ddffce4c98328478 \
                sha256  b89e055074d202aa457d2479a4b33f6a67e3e36a7bf4588755a9c97b3dd824ac \
                size    151712 \
                ${boringssl_hash}${extract.suffix} \
                rmd160  a637a759a70d1edf6d96209f4f3099ca38e794a0 \
                sha256  e3cc306fb25940ac07c05b591375817802359b17a9b20dfcc591fac25cbbb1c0 \
                size    36682480 \
                v${brotli_version}${extract.suffix} \
                rmd160  aef7f7c6f4f1cea3c8dd84563598de17a6118c0e \
                sha256  f9e8d81d0405ba66d181529af42a3354f838c939095ff99930da6aa9cdf6fe46 \
                size    486984 \
                curl-${curl_tag}${extract.suffix} \
                rmd160  e8c839b2a1fc36c44f2d037140ce14d76172b7ed \
                sha256  9d17646d16e6dbad749e4d3e67836eaf9cf6079f540c262cf63b50cf62535b63 \
                size    3198591 \
                v${nghttp2_version}${extract.suffix} \
                rmd160  090697320e35d24c718546175141e9c4d134dbc8 \
                sha256  63dd705b524eec3c843b8e4e24914600a30c2804a113e8e5baeeb9695d09590a \
                size    1055454 \
                nss-${nss_version}-with-nspr-${nspr_version}${extract.suffix} \
                rmd160  86c8ece643fc29db6e4ed2a87eeb732bda7f6893 \
                sha256  21c176bfffb6ec840b5f985c7f8f014682f4a2fb55b06924734172d5c0486dc5 \
                size    73788476

# Use naming which Makefile expects, avoid using broken targets:
post-extract {
    xinstall -d ${worksrcpath}/build

    ln -s ${workpath}/boringssl-${boringssl_hash} ${worksrcpath}/build/boringssl
    ln -s ${workpath}/brotli-${brotli_version} ${worksrcpath}/build/brotli-${brotli_version}
    ln -s ${workpath}/curl-curl-${curl_tag} ${worksrcpath}/build/curl-${curl_version}
    ln -s ${workpath}/nghttp2-${nghttp2_version} ${worksrcpath}/build/nghttp2-${nghttp2_version}
    ln -s ${workpath}/nss-${nss_version} ${worksrcpath}/build/nss-${nss_version}

    # Sources as per https://github.com/google/boringssl/commit/fe0c91e74481e335f434dd6403eeb7ce160fe18d
    copy ${filespath}/err_data.c ${workpath}/boringssl-${boringssl_hash}/crypto/
    copy ${filespath}/file_util.cc ${workpath}/boringssl-${boringssl_hash}/crypto/test/
    copy ${filespath}/file_util.h ${workpath}/boringssl-${boringssl_hash}/crypto/test/
}

set py_ver          3.13
set py_ver_nodot    [string map {. {}} ${py_ver}]
configure.python    ${prefix}/bin/python${py_ver}
set pybindir        ${prefix}/Library/Frameworks/Python.framework/Versions/${py_ver}/bin

depends_build-append \
                port:autoconf \
                port:automake \
                port:bash \
                path:bin/cmake:cmake \
                port:libtool \
                port:ninja \
                path:bin/pkg-config:pkgconfig \
                port:python${py_ver_nodot}
depends_lib-append \
                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle \
                port:libidn2 \
                port:libpsl \
                port:sqlite3 \
                port:zlib \
                port:zstd

if {[variant_isset ff]} {
    depends_build-append \
                port:py${py_ver_nodot}-gyp-next
}

if {[variant_isset chrome]} {
    conflicts_build     openssl
}

compiler.c_standard     2011
compiler.cxx_standard   2011

# Apply patches included in ${name}:
pre-patch {
    if {[variant_isset chrome]} {
        # Cherry-pick from: https://github.com/google/boringssl/commit/3ae23976862fa9036ab52e47c8a22025f5d76ca0
        system -W ${workpath}/boringssl-${boringssl_hash} "patch -p1 < [shellescape ${filespath}/3ae23976862fa9036ab52e47c8a22025f5d76ca0.patch]"
        # Upstream patches:
        system -W ${workpath}/boringssl-${boringssl_hash} "patch -p1 < [shellescape ${worksrcpath}/chrome/patches/boringssl-old-ciphers.patch]"
        system -W ${workpath}/curl-curl-${curl_tag} "patch -p1 < [shellescape ${worksrcpath}/chrome/patches/curl-impersonate.patch]"
        # Our patches:
        system -W ${workpath}/boringssl-${boringssl_hash} "patch -p0 < [shellescape ${filespath}/0001-boringssl.patch]"
    } elseif {[variant_isset ff]} {
        # Upstream patches:
        system -W ${workpath}/curl-curl-${curl_tag} "patch -p1 < [shellescape ${worksrcpath}/firefox/patches/curl-impersonate.patch]"
        # Our patches:
        if {${os.platform} eq "darwin" && ${os.major} < 11} {
            system -W ${workpath}/nss-${nss_version} "patch -p0 < [shellescape ${filespath}/0001-nss.patch]"
        }
    }
}

patchfiles-append   0002-Fix-Makefile.patch

post-patch {
    reinplace "s|__PREFIX__|${prefix}|g" ${worksrcpath}/Makefile.in
    reinplace "s|@CC@|${configure.cc}|g" ${worksrcpath}/Makefile.in

    if {${os.platform} eq "darwin" && ${os.major} < 11} {
        reinplace "s|\$(CURL_CONFIG_FLAGS)|--without-secure-transport|g" ${worksrcpath}/Makefile.in
    }

    reinplace "s|/usr/bin/env bash|${prefix}/bin/bash|" ${workpath}/nss-${nss_version}/nss/build.sh
    reinplace "s|\${GYP:-gyp}|${pybindir}/gyp|" ${workpath}/nss-${nss_version}/nss/build.sh
    reinplace "s|verbose=0|verbose=1|" ${workpath}/nss-${nss_version}/nss/build.sh
}

configure.dir   ${worksrcpath}/build
configure.cmd   ../configure

configure.env-append \
                SHELL=${prefix}/bin/bash

configure.args-append \
                --enable-static \
                --prefix=${destroot}${prefix} \
                --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt \
                --with-zlib=${prefix}

# Avoid nightmarish makefile which cannot even configure sub-projects correctly.
pre-build {
    xinstall -d ${workpath}/brotli-${brotli_version}/out
    system -W ${workpath}/brotli-${brotli_version}/out "${prefix}/bin/cmake \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=\./installed -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_COMPILER=${configure.cc} -DCMAKE_CXX_COMPILER=${configure.cxx} \
        -DCMAKE_C_FLAGS='${configure.cflags}' -DCMAKE_CXX_FLAGS='${configure.cxxflags}' \
        -DCMAKE_OSX_ARCHITECTURES=${configure.build_arch} -DCMAKE_OSX_DEPLOYMENT_TARGET=${macosx_deployment_target} \.\. && \
        ${prefix}/bin/cmake --build \. -v --config Release --target install"

    system -W ${workpath}/nghttp2-${nghttp2_version} "autoreconf --install && ./configure \
        --prefix=${workpath}/nghttp2-${nghttp2_version}/installed --with-pic --enable-lib-only --disable-shared && \
        make V=1 && make install"

    if {[variant_isset chrome]} {
        set ssl_build_dir ${workpath}/boringssl-${boringssl_hash}/build
        xinstall -d ${ssl_build_dir}
        xinstall -d ${ssl_build_dir}/lib
        system -W ${ssl_build_dir} "${prefix}/bin/cmake \
            -DCMAKE_BUILD_TYPE=Release -DCMAKE_POSITION_INDEPENDENT_CODE=on -DOPENSSL_NO_ASM=on -DFIPS=off \
            -DCMAKE_C_COMPILER=${configure.cc} -DCMAKE_CXX_COMPILER=${configure.cxx} \
            -DCMAKE_C_FLAGS='${configure.cflags} -Wno-unknown-warning-option -Wno-stringop-overflow -Wno-array-bounds' \
            -DCMAKE_CXX_FLAGS='${configure.cxxflags} -D__STDC_FORMAT_MACROS' \
            -DCMAKE_OSX_ARCHITECTURES=${configure.build_arch} -DCMAKE_OSX_DEPLOYMENT_TARGET=${macosx_deployment_target} \
            -GNinja \.\. && ${prefix}/bin/ninja"
        ln -sf ${ssl_build_dir}/crypto/libcrypto.a ${ssl_build_dir}/lib/libcrypto.a
        ln -sf ${ssl_build_dir}/ssl/libssl.a ${ssl_build_dir}/lib/libssl.a
        copy ${workpath}/boringssl-${boringssl_hash}/include ${ssl_build_dir}/
    } elseif {[variant_isset ff]} {
        system -W ${workpath}/nss-${nss_version}/nss "CC=${configure.cc} CCC=${configure.cxx} ./build.sh \
            -o --target=${configure.build_arch} --disable-tests --static --python=${configure.python}"
        # We link statically
        fs-traverse dlib ${workpath}/nss-${nss_version}/dist/Release {
            if {[file isfile ${dlib}] && [file extension ${dlib}] == ".dylib"} {
                delete -f ${dlib}
            }
        }
    }
}

build {
    if {[variant_isset chrome]} {
        build.target    chrome-build
    } elseif {[variant_isset ff]} {
        build.target    firefox-build
    }

    system -W ${worksrcpath}/build "make ${build.target} V=1"
}

destroot {
    if {[variant_isset chrome]} {
        destroot.target chrome-install
    } elseif {[variant_isset ff]} {
        destroot.target firefox-install
    }

    system -W ${worksrcpath}/build "make ${destroot.target}"
}

# We rather need the reverse: https://github.com/lexiforest/curl_cffi/pull/186
# But chrome build is broken until this is fixed: https://github.com/lwthiker/curl-impersonate/issues/257
if {![variant_isset chrome]} {
    default_variants +ff
}
