# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0
PortGroup           legacysupport 1.1

github.setup        thepowersgang mrustc aeb58f3dfa1d302bf255ff537d06de288dc4a92c

set rust_version    1.39.0
set rust_version_major [join [lrange [split ${rust_version} .-] 0 1] .]
set rust_version_major_underscore [join [lrange [split ${rust_version} .-] 0 1] _]

# subport mrustc-rust has its own versioning
version             ${rust_version_major}-20251109
revision            0
epoch               1

categories          lang devel
license             MIT
maintainers         {@catap korins.ky:kirill} openmaintainer
description         Alternative implementation of Rust

long_description    In-progress alternative rust compiler. Capable of building \
                    a fully-working copy of rustc, but not suitable for everyday use \
                    (due to inadequate error messages).

set llvm_branch     7
set llvm_distver    ${llvm_branch}.1.0

master_sites-append https://static.rust-lang.org/dist/:rust \
                    https://releases.llvm.org/${llvm_distver}:compiler-rt

distfiles-append    rustc-${rust_version}-src.tar.gz:rust \
                    compiler-rt-${llvm_distver}.src.tar.xz:compiler-rt

checksums           mrustc-${github.version}.tar.gz \
                    rmd160  8d7aa98327687c2784e640b3a41dc83583d5c3d9 \
                    sha256  b5e6b6fb51f4dc1ab712e9fdf53d9822165db1687665a6dac0fc1306cb693232 \
                    size    1383362 \
                    rustc-${rust_version}-src.tar.gz \
                    rmd160  d5b04b87fc336e3be7d592f70de0363aa66622aa \
                    sha256  b4a1f6b6a93931f270691aba4fc85eee032fecda973e6b9c774cd06857609357 \
                    size    152803201 \
                    compiler-rt-${llvm_distver}.src.tar.xz \
                    rmd160  78fb2825ffecd83c8efc7d1d42ca67600b914fd2 \
                    sha256  057bdac0581215b5ceb39edfd5bbef9eb79578f16a8908349f3066251fba88d8 \
                    size    1864248
github.tarball_from archive

set rust_platforms(arm64)  aarch64
set rust_platforms(ppc)    powerpc
set rust_platforms(ppc64)  powerpc64

if {![info exists rust_platforms(${configure.build_arch})]} {
    set rust_platforms(${configure.build_arch}) ${configure.build_arch}
}

set rust_platform   $rust_platforms(${configure.build_arch})

use_configure       no

universal_variant   no

compiler.cxx_standard 2014
compiler.c_standard 2011
compiler.thread_local_storage yes

# Officially tested with GCC 5.4+
compiler.blacklist *gcc-4.*

# See https://github.com/thepowersgang/mrustc/issues/255
compiler.blacklist-append {clang < 1300} {macports-clang-3.[0-9]} macports-clang-4.0

# A C compiler is needed at run-time; it doesn't have to be the same version as
# the C++ compiler used to compile mrustc, but we'll use that one as the default,
# and declare the dependency here
if { [string match macports-clang-* ${configure.compiler}] } {
    depends_run-append \
                    port:[string map {"macports-" ""} ${configure.compiler}]
}
if { [string match macports-gcc-* ${configure.compiler}] } {
    depends_run-append \
                   port:[string map {"macports-gcc-" "gcc"} ${configure.compiler}]

    # GCC is required to be linked with libatomic
    # See: https://github.com/thepowersgang/mrustc/issues/214
    # See: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=81358
    post-patch {
        set script [open "${worksrcpath}/script-overrides/stable-${rust_version}-macos/build_libc.txt" a]
        puts ${script} "cargo:rustc-link-lib=atomic"
        close ${script}
    }
}

post-extract {
    # Prevent it from re-download sources
    ln -sf ${distpath}/rustc-${rust_version}-src.tar.gz ${worksrcpath}/rustc-${rust_version}-src.tar.gz
    ln -sf ${workpath}/rustc-${rust_version}-src ${worksrcpath}/rustc-${rust_version}-src
    touch ${workpath}/rustc-${rust_version}-src/dl-version
    touch ${workpath}/rustc-${rust_version}-src/extracted
    
    ln -sf ${workpath}/compiler-rt-${llvm_distver}.src ${workpath}/compiler-rt
}

pre-patch {
    # Patch mrustc
    # https://github.com/thepowersgang/mrustc/issues/367
    # Notice, we do not set correct arch targets for LLVM at all, since it is not being built.
    system -W ${workpath}/${name}-${github.version} "patch -p1 < [shellescape ${filespath}/mrustc-minicargo.mk-use-external-llvm.patch]"
    # https://github.com/thepowersgang/mrustc/issues/368
    system -W ${workpath}/${name}-${github.version} "patch -p1 < [shellescape ${filespath}/mrustc-codegen_c.cpp-clang-does-not-accept-fno-tree-sra.patch]"
    # Provisional extra fix for powerpc
    system -W ${workpath}/${name}-${github.version} "patch -p1 < [shellescape ${filespath}/mrustc-powerpc-enum.patch]"

    # Patch the Rust source code with the patch included with mrustc
    system -W ${workpath}/rustc-${rust_version}-src "patch -p0 < [shellescape ${worksrcpath}/rustc-${rust_version}-src.patch]"
}

patch.dir           ${workpath}/rustc-${rust_version}-src
patch.pre_args-replace  -p0 -p1

patchfiles-append   0001-Restore-LLVM-7-compatibility.patch

# Add powerpc into rustc:
patchfiles-append   0002-Add-powerpc-targets-for-darwin.patch \
                    0003-rustc-ap-rustc_target.patch \
                    0004-vendor-libc.patch

# Unbreak llvm linking:
patchfiles-append   0005-librustc_llvm-fix-linking.patch

if {${configure.cxx_stdlib} ne "libc++"} {
    patchfiles-append \
                    0006-do-not-force-libcxx.diff
}

# patches as git tree: https://github.com/catap/rust-legacy-bootstrap/tree/1.54.0
patchfiles          0001-Introduced-support-of-MACPORTS_LEGACY_SUPPORT_.patch \
                    0002-libgit2-sys-use-src-instead-.git-as-vendored-indicat.patch \
                    0003-libssh2-sys-use-src-instead-.git-as-vendored-indicat.patch

if {${os.platform} eq "darwin" && ${os.major} < 12} {
    patchfiles-append \
                    0004-macOS-10.7-Switch-to-OpenSSL.patch
}

if {${os.platform} eq "darwin" && ${os.major} < 11} {
    patchfiles-append \
                    0005-macOS-10.6-enforce-emulated-TLS.patch \
                    0006-macOS-10.6-Fix-pthread_cond_destroy.patch

    set libemutls ${workpath}/libemutls
    set libemutls_a ${libemutls}/libemutls.a
    set emutls_c ${workpath}/compiler-rt/lib/builtins/emutls.c

    build.env-append \
                    MRUSTC_LIBDIR=${libemutls}

    post-patch {
        set script [open "${worksrcpath}/script-overrides/stable-${rust_version}-macos/build_std.txt" a]
        puts ${script} "cargo:rustc-cfg=target_enforce_emulated_tls"
        puts ${script} "cargo:rustc-link-lib=static=emutls"
        close ${script}
    }

    pre-build {
        file mkdir ${libemutls}
        system "${configure.cc} ${configure.cflags} [get_canonical_archflags cc] -c -o ${libemutls_a} ${emutls_c}"

        set libemutls_s ${worksrcpath}/run_rustc/output-${rust_version}/prefix-s/lib/rustlib/${rust_platform}-apple-darwin/lib
        file mkdir ${libemutls_s}
        file copy -force {*}[glob ${libemutls}/*] ${libemutls_s}
    }
}

legacysupport.use_static    yes

set cxxflags        "${configure.cxxflags}"
set ldflags         "${configure.ldflags}"

if { ${os.platform} eq "darwin" && ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
    set cxxflags    "${cxxflags} [legacysupport::get_cpp_flags]"
    set ldflags     "${ldflags} [legacysupport::get_library_link_flags]"

    post-patch {
        set script [open "${worksrcpath}/script-overrides/stable-${rust_version}-macos/build_std.txt" a]
        puts ${script} "cargo:rustc-link-lib=static=MacportsLegacySupport"
        puts ${script} "cargo:rustc-link-search=native=${prefix}/lib"
        close ${script}
    }
}

# mrustc produces huge C file with size near of 1Gb
# and run a few clang to compile it in parallel requires significant amount of RAM.
# build.env-append    PARLEVEL=${build.jobs}

set llvm_version        ${llvm_branch}.0
set llvm_port           ${llvm_version}

platform darwin powerpc {
    set llvm_version    ${llvm_branch}.1.1
    set llvm_port       powerpc
}

build.env-append    CC=${configure.cc} \
                    CXX=${configure.cxx} \
                    CFG_DEFAULT_LINKER=${configure.cxx} \
                    "CXXFLAGS_EXTRA=${cxxflags}" \
                    "LINKFLAGS_EXTRA=${ldflags}" \
                    RUSTC_VERSION=${rust_version} \
                    MRUSTC_TARGET_VER=${rust_version_major}

# mrustc had hardcoded x86_64 as target platform
build.env-append    RUSTC_TARGET=${rust_platform}-apple-${os.platform}
post-patch {
# Does not do anything for earlier rustc versions, therefore commented out:
#    reinplace "s|STD_ENV_ARCH=x86_64|STD_ENV_ARCH=${rust_platform}|g" \
#        ${worksrcpath}/script-overrides/stable-${rust_version}-macos/build_std.txt
    reinplace "s|@PREFIX@|${prefix}|g" \
        ${worksrcpath}/minicargo.mk ${worksrcpath}/run_rustc/Makefile
    reinplace "s|@LLVM_V@|${llvm_version}|g" \
        ${worksrcpath}/minicargo.mk ${worksrcpath}/run_rustc/Makefile

    # These are empty files, but the build wants them:
    foreach f {build_rustc_asan.txt build_rustc_lsan.txt build_rustc_msan.txt build_rustc_tsan.txt} {
        system "touch ${worksrcpath}/script-overrides/stable-${rust_version}-macos/${f}"
    }
}

# set output_dir      output-${rust_version}
set output_dir      output

# Step 1: building mrustc, its libs and minicargo
if {${subport} eq ${name}} {
    depends_lib-append \
                    port:zlib

    # Parallel building of libs may cause random failure
    # See: https://github.com/thepowersgang/mrustc/issues/225
    build.target    -f minicargo.mk -j${build.jobs} bin/mrustc bin/minicargo && \
                    ${build.cmd} -j${build.jobs} -C tools/dump_hirfile && \
                    ${build.cmd} -j${build.jobs} -C tools/standalone_miri && \
                    ${build.cmd} -j1 -f minicargo.mk LIBS

    destroot {
        xinstall -d ${destroot}${prefix}/libexec/mrustc/bin

        xinstall -m 0755 -W ${worksrcpath}/bin mrustc minicargo dump_hirfile standalone_miri \
            ${destroot}${prefix}/libexec/mrustc/bin

        # install all .rlib, .rlib.hir and .rlib.o to lib/
        xinstall -d ${destroot}${prefix}/libexec/mrustc/lib
        foreach f [glob -directory ${worksrcpath}/${output_dir} lib*.rlib lib*.rlib.hir lib*.rlib.o] {
            xinstall -m 0644 $f ${destroot}${prefix}/libexec/mrustc/lib
        }

        if {${os.platform} eq "darwin" && ${os.major} < 11} {
            foreach f [glob -directory ${libemutls}/ lib*] {
                xinstall -m 0644 $f ${destroot}${prefix}/libexec/mrustc/lib
            }
        }

        # create a wrapper to run mrustc without setup any ENV
        set mrustc [open "${destroot}${prefix}/bin/mrustc" w 0755]
        puts ${mrustc} "#!/bin/sh"
        puts ${mrustc} "export CC=\"\$\{CC:-${configure.cc}\}\""
        puts ${mrustc} "export MRUSTC_TARGET_VER=\"\$\{MRUSTC_TARGET_VER:-${rust_version_major}\}\""
        puts ${mrustc} "export MRUSTC_LIBDIR=\"\$\{MRUSTC_LIBDIR:-${prefix}/libexec/mrustc/lib\}\""
        puts ${mrustc} "${prefix}/libexec/mrustc/bin/mrustc \$@"
        close ${mrustc}
    }
}

# Step 2: build a rustc and cargo by mrustc
subport mrustc-rust {
    PortGroup       openssl 1.0

    version         ${rust_version}
    revision        0
    epoch           1

    description     Rust and cargo which was compiled by mrustc.
    long_description {*}${description}

    # cargo is too old to be used with 3.0
    openssl.branch  1.1

    depends_build-append \
                    port:cctools \
                    path:bin/git:git \
                    port:gmake \
                    port:mrustc

    depends_lib-append \
                    port:curl \
                    port:libffi \
                    port:llvm-${llvm_port}

    set py_v        2.7
    set py_v_nodot  [string map {. {}} ${py_v}]

    # Use the system python27 if present
    if {${os.platform} eq "darwin" && ${os.major} >= 11 && ${os.major} <= 21} {
        set pythonfullpath   /usr/bin/python${py_v}
    } else {
        set pythonfullpath   ${prefix}/bin/python${py_v}
        depends_build-append port:python${py_v_nodot}
    }

    build.env-append \
                    OPENSSL_DIR=[openssl::install_area] \
                    OPENSSL_INCLUDE_DIR=[openssl::include_dir] \
                    OPENSSL_LIB_DIR=[openssl::lib_dir] \
                    MINICARGO=${prefix}/libexec/mrustc/bin/minicargo \
                    "MINICARGO_FLAGS=--features curl-sys/force-system-lib-on-osx" \
                    MRUSTC=${prefix}/libexec/mrustc/bin/mrustc

    # Parallel building on some stages may cause random failure
    # See: https://github.com/thepowersgang/mrustc/issues/225
    # So:
    #  - Bootstrap everything in single mode
    build.target    ${build.cmd} -j1 -C run_rustc all

    destroot {
        xinstall -d ${destroot}${prefix}/libexec/${subport}/bin

        xinstall -m 0755 -W ${worksrcpath}/run_rustc/${output_dir}/prefix/bin \
            cargo rustc_binary \
            ${destroot}${prefix}/libexec/${subport}/bin

        set rustc [open "${destroot}${prefix}/libexec/${subport}/bin/rustc" w 0755]
        puts ${rustc} "#!/bin/sh"
        puts ${rustc} "# macOS never uses LD_LIBRARY_PATH, but rust uses it to find the path to its driver"
        puts ${rustc} "LD_LIBRARY_PATH=\"${prefix}/libexec/${subport}/lib\" ${prefix}/libexec/${subport}/bin/rustc_binary \$@"
        close ${rustc}

        ln -sf ${prefix}/libexec/${subport}/bin/rustc ${prefix}/bin/${subport}

        xinstall -d ${destroot}${prefix}/libexec/${subport}/lib
        file copy ${worksrcpath}/run_rustc/${output_dir}/prefix/lib/rustlib \
            ${destroot}${prefix}/libexec/${subport}/lib

        system -W ${destroot}${prefix}/libexec/${subport}/bin \
            "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-rustc/${rust_platform}-apple-${os.platform}/release/deps/librustc_driver.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/librustc_driver.dylib ./rustc_binary"

        system -W ${destroot}${prefix}/libexec/${subport}/bin \
            "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std2/${rust_platform}-apple-${os.platform}/release/deps/libtest.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libtest.dylib ./rustc_binary"

        system -W ${destroot}${prefix}/libexec/${subport}/bin \
            "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std2/${rust_platform}-apple-${os.platform}/release/deps/libstd.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libstd.dylib ./rustc_binary"

        system -W ${destroot}${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib \
            "install_name_tool -id ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/librustc_driver.dylib ./librustc_driver.dylib"

        system -W ${destroot}${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib \
            "install_name_tool -id ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libstd.dylib ./libstd.dylib"

        system -W ${destroot}${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib \
            "install_name_tool -id ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libtest.dylib ./libtest.dylib"

        foreach f [ exec find ${destroot}${prefix}/libexec/${subport}/lib -name "*.dylib" ] {
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-rustc/${rust_platform}-apple-${os.platform}/release/deps/librustc_driver.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/librustc_driver.dylib $f"
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-rustc/${rust_platform}-apple-${os.platform}/release/deps/librustc_codegen_llvm.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/librustc_codegen_llvm.dylib $f"
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-rustc/${rust_platform}-apple-${os.platform}/release/deps/libtest.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libtest.dylib $f"
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-rustc/${rust_platform}-apple-${os.platform}/release/deps/libstd.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libstd.dylib $f"

            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std/${rust_platform}-apple-${os.platform}/release/deps/libtest.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libtest.dylib $f"
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std/${rust_platform}-apple-${os.platform}/release/deps/libstd.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libstd.dylib $f"

            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std2/${rust_platform}-apple-${os.platform}/release/deps/libtest.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libtest.dylib $f"
            system "install_name_tool -change ${worksrcpath}/run_rustc/${output_dir}/build-std2/${rust_platform}-apple-${os.platform}/release/deps/libstd.dylib ${prefix}/libexec/${subport}/lib/rustlib/${rust_platform}-apple-${os.platform}/lib/libstd.dylib $f"
        }
    }

    livecheck.type  none
}

# Let add some basic tests for mrustc port
if {${subport} eq ${name}} {
    test.env        {*}${build.env} MRUSTC_LIBDIR=${output_dir}
    test.run        yes
    test.target     test
}
