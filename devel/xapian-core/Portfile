# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                xapian-core
version             1.4.30
categories          devel
maintainers         {ryandesign @ryandesign} openmaintainer
license             GPL-2+

description         Xapian search library
long_description    Xapian is a highly adaptable toolkit which allows \
                    developers to easily add advanced indexing and search \
                    facilities to their own applications. It supports \
                    the Probabilistic Information Retrieval model and also \
                    supports a rich set of boolean query operators.

homepage            https://xapian.org
master_sites        https://oligarchy.co.uk/xapian/${version}/
dist_subdir         xapian
use_xz              yes

compiler.cxx_standard \
                    2011

if {${subport} eq ${name}} {
    revision                    0

    # version.h does not get generated properly when there are multiple -arch flags
    PortGroup                   muniversal 1.0

    checksums                   rmd160  1b85267c046e78c413e5a83779dd75b0e8ec5a73 \
                                sha256  4edf9989499e8bc95085c9f7108ed41d69546c34c6eea81da0fa22d95043bf72 \
                                size    3252336

    platform darwin 8 {
        patchfiles-append       tiger-needs-header.diff
    }
    # TODO: Fix xapian-config to not require the .la file
    # /opt/local/bin/xapian-config --ltlibs --cxxflags
    # /opt/local/bin/xapian-config: Can't find libxapian.la to link against.
    destroot.delete_la_files    no

    depends_lib                 port:zlib

    post-build {
        if {[variant_exists universal] && [variant_isset universal]} {
            foreach arch ${universal_archs_to_use} {
                # xapian-config records build directory, which interferes with universal merge
                reinplace "s|${worksrcpath}-${arch}|${worksrcpath}|g" ${worksrcpath}-${arch}/xapian-config
            }
        }
    }

    livecheck.type              regex
    livecheck.url               ${homepage}/download
    livecheck.regex             ${name}-(\[0-9\]+\\.\[0-9\]*\[02468\](\\.\[0-9\]+)*)[quotemeta ${extract.suffix}]
} else {
    livecheck.type              none
}

subport xapian-omega {
    revision                    0
    checksums                   rmd160  cb39011eb2bf259c29d176cec901a5a58f156815 \
                                sha256  b3faf202efd11ab6eb749bdd47f639b3dedc781eb0d579edafc80cd1340a461d \
                                size    588760

    description                 Web application using the Xapian library
    long_description            Xapian Omega is an application built on \
                                the Xapian library, consisting of indexers \
                                and a CGI search frontend.

    homepage                    ${homepage}/docs/omega/overview.html

    distname                    xapian-omega-${version}

    depends_lib                 port:libiconv \
                                port:libmagic \
                                port:pcre2 \
                                port:xapian-core \
                                port:zlib

    configure.args              --with-iconv
}

if {${subport} ni [list ${name} xapian-omega]} {
    checksums                   rmd160  d624ac567696e33d47bddd405dd8f031e2566fb2 \
                                sha256  abf46acd62b9647157f36b636e1844e7e3bd9457e42312c40029ed63c602379c \
                                size    1121316

    homepage                    ${homepage}/docs/bindings

    distname                    xapian-bindings-${version}

    depends_lib                 port:xapian-core

    configure.args              --without-csharp \
                                --without-java \
                                --without-lua \
                                --without-perl \
                                --without-python \
                                --without-python3 \
                                --without-php \
                                --without-ruby \
                                --without-tcl
}

# Java
subport xapian-bindings-java {
    PortGroup                   java 1.0

    revision                    0

    description                 Xapian bindings for Java
    long_description            {*}${description}

    # Required java version
    java.version                6+
    # JDK port to install if required java not found
    java.fallback               openjdk8

    configure.args-replace      --without-java --with-java

    # jni destination violates the mtree layout
    destroot.violate_mtree  yes

    # https://trac.xapian.org/ticket/774
    post-destroot {
        file mkdir ${destroot}/Library/Java/Extensions
        move ${destroot}/${worksrcpath}/java/built/libxapian_jni.jnilib \
            ${destroot}/Library/Java/Extensions/libxapian_jni.jnilib
    }
}

# Perl
foreach v {5.28 5.30 5.32 5.34 5.36 5.38 5.40} {
    subport xapian-bindings-perl${v} "
        revision                0

        description             Xapian bindings for Perl ${v}
        long_description        \{*\}\${description}

        depends_lib-append      port:perl${v}

        configure.env-append    PERL=${prefix}/bin/perl${v}
        configure.args-replace  --without-perl --with-perl

        post-destroot {
            move ${destroot}${prefix}/share/doc/xapian-bindings/perl ${destroot}${prefix}/share/doc/xapian-bindings/perl${v}
        }
    "
}

# PHP
foreach v {8.0 8.1 8.2 8.3} {
    set v_no_dot [string map {. {}} ${v}]
    set php php${v_no_dot}
    subport ${php}-xapian "
        revision                0

        description             Xapian bindings for PHP ${v}
        long_description        \{*\}\${description}

        depends_lib-append      port:${php}

        configure.env-append    PHP_CONFIG=${prefix}/bin/php-config${v_no_dot}
        configure.args-replace  --without-php --with-php

        post-destroot {
            move ${destroot}${prefix}/share/doc/xapian-bindings/php ${destroot}${prefix}/share/doc/xapian-bindings/${php}
            xinstall -m 0755 -d ${destroot}${prefix}/var/db/${php}
            set fp \[open ${destroot}${prefix}/var/db/${php}/xapian.ini w\]
            puts \${fp} {; Do not edit this file; it is automatically generated by MacPorts. Any changes}
            puts \${fp} {; you make will be lost if you upgrade, uninstall or deactivate ${subport}.}
            puts \${fp} \{; To configure ${php}, edit ${prefix}/etc/${php}/php.ini.\}
            puts \${fp} {extension=xapian.so}
            close \${fp}
        }
    "
}

# Python
foreach v {3.10 3.11 3.12 3.13 3.14} {
    set v_no_dot [string map {. {}} ${v}]
    if {[vercmp ${v} 3] >= 0} {
        set suffix 3
    } else {
        set suffix {}
    }
    subport xapian-bindings-python${v_no_dot} "
        revision                0

        description             Xapian bindings for Python ${v}
        long_description        \{*\}\${description}

        depends_build-append    port:py${v_no_dot}-sphinx

        depends_lib-append      port:python${v_no_dot}

        configure.env-append    PYTHON${suffix}=${prefix}/bin/python${v}
        configure.args-replace  --without-python${suffix} --with-python${suffix}

        post-destroot {
            move ${destroot}${prefix}/share/doc/xapian-bindings/python${suffix} ${destroot}${prefix}/share/doc/xapian-bindings/python${v_no_dot}
        }
    "
}

# Ruby
foreach v {2.7 3.0 3.1 3.2 3.3 3.4} {
    set v_no_dot [string map {. {}} ${v}]
    subport xapian-bindings-ruby${v_no_dot} "
        revision                0

        description             Xapian bindings for Ruby ${v}
        long_description        \{*\}\${description}

        depends_lib-append      port:ruby${v_no_dot}

        configure.env-append    RUBY=${prefix}/bin/ruby${v}
        configure.args-replace  --without-ruby --with-ruby

        post-destroot {
            move ${destroot}${prefix}/share/doc/xapian-bindings/ruby ${destroot}${prefix}/share/doc/xapian-bindings/ruby${v_no_dot}
        }
    "
}
